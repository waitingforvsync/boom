addr = &70
dest = &72
bitstream = &74
fixedbits = &75
numblocks = &76
temp = &78
count = &79


.getbit
    \\ Gets one bit from the bitstream
    \\ On exit:
    \\   A preserved
    \\   X preserved
    \\   Y corrupted
    \\   C = bit
    LSR bitstream
    BNE samebyte

compressed_lz_src = P%+1
    LDY &FFFF
    INC compressed_lz_src
    BNE P%+5
    INC compressed_lz_src+1
    STY bitstream
    ROR bitstream
.samebyte
    RTS


.get8bits
    LDX #7
.getbits
    \\ Gets a value composed of a specified number of bits from the bitstream
    \\ Note: bits must be stored reversed (most significant bit first)
    \\ On entry:
    \\   X = number of bits minus 1
    \\ On exit:
    \\   A = value lsb
    \\   X = &FF
    \\   Y corrupted
    \\   temp = value msb
    \\   C undefined
    JSR getbit
    ROL A
    ROL temp
    DEX
    BPL getbits
    RTS


.getgammavalue
    \\ Gets an Elias Gamma value from the bitstream
    \\ On exit:
    \\   A = value lsb
    \\   X = &FF
    \\   Y corrupted
    \\   temp = value msb
    \\   C undefined
    LDX #254
    LDA #1
.getgammaloop
    JSR getbit
    INX
    BCC getgammaloop
    BPL getbits
    RTS


.gethybridvaluehi
    \\ Gets the top bits of a hybrid (gamma+fixed bits) value from the bitstream
    \\ Call LDX #fixedbits-1:JSR getbits afterwards.
    \\ On exit:
    \\   A = value
    \\   X = &FF
    \\   C = 1
    LDA #0
    STA temp
    JSR getgammavalue
    SEC
    SBC #1
    RTS


.getcount
    LDA numblocks:BNE skip
    DEC numblocks+1:BPL skip
    PLA:PLA:RTS
    .skip
    DEC numblocks

    JSR getgammavalue
    STA count
    CMP #0
    RTS


.updatedest
    TYA
    ADC dest
    STA dest
    BCC updatedest2
    INC dest+1
    .updatedest2
    DEC count
    RTS


.decompress_lz
    LDA #1
    STA bitstream

    JSR gethybridvaluehi
    JSR get8bits
    STA numblocks
    LDA temp
    STA numblocks+1
    
    LDA #0
    LDX #2:JSR getbits
    STA fixedbits

.decompressloop
    JSR getcount
    BEQ reference

.literal
    JSR get8bits
    LDY #0:STA (dest),Y
    SEC
    JSR updatedest
    BNE literal

    JSR getcount
    BEQ literal

.reference
    JSR gethybridvaluehi
    LDX fixedbits
    JSR getbits

    EOR #255:ADC dest:STA addr
    LDA dest+1:SBC temp:STA addr+1

    JSR getgammavalue
    STA temp

    LDY #255
.copyloop
    INY
    LDA (addr),Y
    STA (dest),Y
    CPY temp
    BNE copyloop

    JSR updatedest
    BNE reference
    BEQ decompressloop


PRINT "Length of decompress: ", P%-getbit, "bytes"
