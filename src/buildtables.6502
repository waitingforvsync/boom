\\ Some compact code to build all the tables required.

.buildtables

\\ Build multiplication tables
\\ This builds the f(x) = x*x / 4 tables required for the fast multiplication.
\\ It also builds parallel tables of f(x) = (x-255)*(x-255) / 4, used as an optimization.
\\
\\ This makes use of the fact that
\\   f(0) = 0
\\   f(x+1) = floor[(x+1)/2] + f(x)

.buildmultables
{
	LDA #0:TAX:TAY
	.buildmultabloop
	CLC
	.buildmultabloop2
	TYA:ADC #0
	.setmultabhi STA multabhi,X
	CMP #&40	; when X>=256, f(X)>=&4000
	TAY:TXA		; A+256*C = table index
	ROR A
	.setadd ADC #0
	STA setadd+1
	INX
	.setmultablo STA multablo,X
	BNE buildmultabloop2
	INC setmultabhi+2
	INC setmultablo+2
	INY
	BNE buildmultabloop
	
	DEY
	.buildmultabnegloop
	LDA multablo,X:STA multabneglo,Y
	LDA multabhi,X:STA multabneghi,Y
	LDA multablo+1,X:STA multabneglo+&100,X
	LDA multabhi+1,X:STA multabneghi+&100,X
	DEY:INX
	BNE buildmultabnegloop
}

\\ Build reciprocal tables
\\ This builds the 16-bit result of 65536/x.
\\ This is just a standard 6502 division routine specialised to perform (&10000/x)

.buildreciptables
{
	LDX #0                    ; denominator
	.reciploop
	STX denom+1
	LDA #1                    ; top byte of the numerator
	LDY #16                   ; because result is 16-bit
	.divloop
	ASL A:BCS subtract
	.denom CMP #0:BCC skip    ; this comes from X
	.subtract
	SBC denom+1:SEC
	.skip
	ROL reciptablo,X:ROL reciptabhi,X
	DEY:BNE divloop
	INX:BNE reciploop
}

\\ Generate sine table between 0 and 180 degrees
\\ This will be negated or accessed at 90 degree phase for cosines or sin(>180)

.buildsintable
{
	LDA #0:TAX:LDY #sindeltasend-sindeltas-1
	.loop1
	PHA
	LDA sindeltas,Y:PHA:AND #7:STA temp
	PLA:LSR A:LSR A:LSR A:STA temp+1
	PLA
	.loop2
	CLC:ADC temp:STA sintab,X:INX
	DEC temp+1:BPL loop2
	DEY:BPL loop1

	LDY #63:.duplicate
	LDA sintab,Y:STA sintab,X
	INX:DEY:BNE duplicate
}

RTS


\\ Run-length encoded deltas for sine values, accessed backwards
\\ This builds a table of floor(SIN(n/128*PI)*256+0.5) for n=0..127
\\ 256 is stored as 0 and handled specially when multiplying.
.sindeltas
	EQUB 1*8+0, 1, 0, 4*8+1, 2, 1, 4*8+2, 1*8+3
	EQUB 2, 2*8+3, 4, 3, 4, 3, 3*8+4, 5
	EQUB 1*8+4, 8*8+5, 6, 5, 1*8+6, 5, 10*8+6, 7
	EQUB 2*8+6, 7, 6, 0
.sindeltasend
